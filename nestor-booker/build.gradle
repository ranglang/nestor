import java.nio.file.Paths

apply plugin: 'de.undercouch.download'

ext['selenium.version'] = '3.11.0'

def chromeDriverVersion = '2.37'
def chromeHeadlessVersion = 'v1.0.0-35' //chromium 64.0.3282.119 (stable channel) for amazonlinux:2017.03
def tempDir = Paths.get(projectDir.path, 'bin').toString()
def resourcesDir = 'src/main/resources/bin/'

task downloadChromeDriver(type: Download) {
    src "https://chromedriver.storage.googleapis.com/${chromeDriverVersion}/chromedriver_linux64.zip"
    dest new File(tempDir, "chromedriver_linux64_${chromeDriverVersion}.zip")
    overwrite false
}

task downloadAndUnzipChromeDriver(dependsOn: downloadChromeDriver, type: Copy) {
    from zipTree(downloadChromeDriver.dest)
    into resourcesDir
}

task downloadChromeHeadless(type: Download) {
    src "https://github.com/adieuadieu/serverless-chrome/releases/download/${chromeHeadlessVersion}/stable-headless-chromium-amazonlinux-2017-03.zip"
    dest new File(tempDir, "stable-headless-chromium-amazonlinux-2017-03_${chromeHeadlessVersion}.zip")
    overwrite false
}

task downloadAndUnzipChromeHeadless(dependsOn: downloadChromeHeadless, type: Copy) {
    from zipTree(downloadChromeHeadless.dest)
    into resourcesDir
}

processResources.dependsOn(downloadAndUnzipChromeDriver, downloadAndUnzipChromeHeadless)

dependencies {
    compile "org.seleniumhq.selenium:selenium-java"
    compile "com.amazonaws:aws-java-sdk-ses:$awsSdkVersion"
}